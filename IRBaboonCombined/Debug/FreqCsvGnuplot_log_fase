# â€™
reset session

set style line 1 lw 2 lc rgb "#80AA00B8" 
set style line 2 lw 2 lc rgb "#502BB033" 
set style line 3 lw 2 lc rgb "#70FF4F00" 
set style line 4 lw 2 lc rgb "#900000FF" 
set style line 5 lw 2 lc rgb "#70FF78DA" 
set style line 6 lw 2 lc rgb "#70000000" 
set style line 7 lw 2 lc rgb "#70FFAB11" 
set style line 8 lw 2 lc rgb "#50D8D400"
set style increment user

set key autotitle columnhead 
set key left 
set datafile separator "\t"
set autoscale
set grid
set format x "%1.s%c" 

set logscale x
set xrange [10:24500]
set xtics (20, 50, 100, 200, 400, 600, 800, 1000, 2000, 4000, 6000, 8000, 10000, 12000, 16000, 20000, 24000)
set mxtics 10
set xlabel "Frequency [Hz]"

set ylabel "Amplitude [dB]"

set y2tics -5/4*pi, pi/4, 5/4*pi
set y2range [-4:4]
set ytics nomirror
set y2label "Phase [rad]"

set term qt size 1200, 600

list = system('ls *.csv')
plot for [file in list] file u 1:3 w li, for [file in list] file u 1:5 axes x1y2 dt "-" w li


